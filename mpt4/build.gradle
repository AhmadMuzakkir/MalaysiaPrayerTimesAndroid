apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.i906.mpt"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 3979
        versionName "2.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        mock {
        }

        prod {
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            exceptionFormat 'full'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def finalName = "mpt-" + variant.versionName + "-" +
                    variant.productFlavors[0].name + "-" + variant.buildType.name + ".apk";
            output.outputFile = new File(output.outputFile.parent, finalName)
        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }
}

dependencies {
    def BUTTERKNIFE_VERSION = rootProject.ext.BUTTERKNIFE_VERSION;
    def DAGGER_VERSION = rootProject.ext.DAGGER_VERSION;
    def RXANDROID_VERSION = rootProject.ext.RXANDROID_VERSION;
    def SUPPORT_VERSION = rootProject.ext.SUPPORT_VERSION;

    compile project(':api-client');

    // Android Support Libraries
    compile "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    compile "com.android.support:design:$SUPPORT_VERSION"

    // Butterknife
    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    apt "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    // Dagger
    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    provided 'javax.annotation:jsr250-api:1.0'

    // RX
    compile "io.reactivex:rxandroid:$RXANDROID_VERSION"

    // Misc
    compile 'com.heinrichreimersoftware:material-intro:1.5.4'

    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support:support-annotations:24.0.0'
}
