import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

def STRING = "String"
def GIT_SHA = "GIT_SHA"
def BUILD_TIME = "BUILD_TIME"

def gitSha() {
    def gitpath = "git"
    def Properties localProps = new Properties()
    def localPropsFile = rootProject.file('local.properties')
    localProps.load(new FileInputStream(localPropsFile))
    def localgitpath = localProps['GIT_PATH']
    if (localgitpath != null && !localgitpath.toString().isEmpty()) gitpath = localgitpath
    String ex = gitpath + ' rev-parse --short HEAD'
    return ex.execute().text.trim()
}

def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

def buildCount() {
    def Properties versionProps = new Properties()
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.exists()) versionProps.load(new FileInputStream(versionPropsFile))
    def code = (versionProps['VERSION_CODE'] ?: "0").toInteger() + 1
    versionProps['VERSION_CODE'] = code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)
    return code
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    def build_count = buildCount()

    defaultConfig {
        applicationId "com.i906.mpt"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode build_count
        versionName "2.0.0"

        buildConfigField STRING, GIT_SHA, "\"${ gitSha() }\""
        buildConfigField STRING, BUILD_TIME, "\"${ buildTime() }\""
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {

        debug {
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
            ext.enableCrashlytics = false
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile project(':mpt-extension:extension')
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'io.reactivex:rxandroid:0.25.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.google.android.gms:play-services-location:7.5.0'
    compile 'pl.charmas.android:android-reactive-location:0.6@aar'
    compile 'com.github.frankiesardo:linearlistview:1.0.1@aar'
    compile 'com.github.castorflex.smoothprogressbar:library-circular:1.1.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    compile 'com.opencsv:opencsv:3.4'
    compile 'com.pushtorefresh.storio:sqlite:1.0.0'
}
